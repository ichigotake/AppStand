apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 20

        def version = readVersion(file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()

        buildConfigField 'String', 'BASE_PACKAGE_NAME', "\"${project.properties.basePackageName}\""
    }

    def signingFile = file(project.properties.storeFile)
    def signingAuthority = signingFile.exists()
    signingConfigs {
        release {
            if (signingAuthority) {
                storeFile signingFile
                storePassword project.properties.storePassword
                keyAlias project.properties.storeAlias
                keyPassword project.properties.keyPassword
            }
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingAuthority) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        free {
            packageName 'net.ichigotake.appstand.free'
        }

        pro {
            packageName 'net.ichigotake.appstand.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:20.0.0'
}
